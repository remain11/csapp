#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
	irmovq $1, %r8
	rrmovq %rdx, %r9
	andq %r8, %r9
    je Even  
Odd:
	mrmovq (%rdi), %r8
	iaddq $-1, %rdx
	rmmovq %r8, (%rsi)
	iaddq $8, %rdi		# src++
	iaddq $8, %rsi		# dst++
	andq %r8, %r8
	jle Even
	iaddq $1, %rax
Even:
	andq %rdx,%rdx		# len <= 0?
	jle Done		# if so, goto Done:	
Loop:	
	mrmovq (%rdi), %r8	# read val from src...
	mrmovq 8(%rdi), %r9
	rmmovq %r8, (%rsi)	# ...and store it to dst
	rmmovq %r9, 8(%rsi)
	andq %r8, %r8		# val <= 0?
	jle Test    	# if so, goto Test:
	iaddq $1, %rax		# count++
Test:
	andq %r9, %r9
	jle Npos
	iaddq $1, %rax
Npos:	
	iaddq $16, %rdi		# src+=2
	iaddq $16, %rsi		# dst+=2
	iaddq $-2, %rdx		# len-=2
	jg Loop			# if so, goto Loop:


##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
